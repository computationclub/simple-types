grammar Parser.Grammar

expression        <-  _* (statement / type) _*               %term_expression

# ==============================================================================

lambda            <-  "λ" / "\\"
arrow             <-  "→" / "->"
darrow            <-  "⇒" / "=>"
times             <-  "×" / "*"
_                 <-  [ \n\r\t]

# ==============================================================================

statement         <-  sequence
                   /  control

control           <-  abstraction
                   /  if_expr
                   /  let_binding
                   /  sum_case
                   /  var_case
                   /  term

term              <-  ascribed_term
                   /  ascribable

ascribable        <-  application
                   /  app_base

app_base          <-  builtin_fn
                   /  atom

builtin_fn        <-  succ_expr
                   /  pred_expr
                   /  iszero_expr
                   /  nil_expr
                   /  cons_expr
                   /  isnil_expr
                   /  head_expr
                   /  tail_expr

atom              <-  projection
                   /  proj_base

proj_base         <-  paren_expr
                   /  true_expr
                   /  false_expr
                   /  zero_expr
                   /  unit_expr
                   /  inject_expr
                   /  variable
                   /  pair_expr
                   /  tuple_expr
                   /  record_expr

# ==============================================================================

paren_expr        <-  "(" _* statement _* ")"                     %paren_term

sequence          <-  control _* ";" _* statement                 %term_seq

ascribed_term     <-  ascribable _+ "as" _+ type                  %term_ascribe

                   /  "<" _* label _* "=" _* control _* ">"
                      _+ "as" _+ type                             %term_tagged

# ==============================================================================

application       <-  app_base (_+ atom)+                         %term_app

abstraction       <-  lambda _* variable _* ":" _* type
                      _* "." _* control                           %term_abs

variable          <-  [a-z] ![a-z]                                %term_var

# ==============================================================================

true_expr         <-  "true"                                      %term_true
false_expr        <-  "false"                                     %term_false
if_expr           <-  "if" _+ term _+ "then" _+
                      term _+ "else" _+ term                      %term_if

# ==============================================================================

zero_expr         <-  "0"                                         %term_zero
succ_expr         <-  "succ" _+ atom                              %term_succ
pred_expr         <-  "pred" _+ atom                              %term_pred
iszero_expr       <-  "iszero" _+ atom                            %term_iszero

# ==============================================================================

unit_expr         <-  "unit"                                      %term_unit

# ==============================================================================

let_binding       <-  "let" _+ variable _* "=" _*
                      term _+ "in" _+ term                        %term_let

# ==============================================================================

projection        <-  proj_base (_* "." _* proj_field)+           %term_project
proj_field        <-  label / [1-9] [0-9]*

pair_expr         <-  "{" _* control _* "|" _* control _* "}"     %term_pair

tuple_expr        <-  "{" _* control _* ("," _* control _*)+ "}"  %term_tuple

record_expr       <-  "{" record_pair ("," record_pair)* "}"      %term_record
record_pair       <-  _* label _* "=" _* control _*
label             <-  [A-Za-z0-9_-]+

# ==============================================================================

inject_expr       <-  ("inl" / "inr") _+ ascribable               %term_inject

sum_case          <-  "case" _+ term _+ "of" _+
                      "inl" _+ sum_clause _* "|" _*
                      "inr" _+ sum_clause                         %term_sum_case

sum_clause        <-  variable _* darrow _* term                  %term_sum_clause

var_case          <-  "case" _+ term _+ "of" _+
                      var_clause (_* "|" _* var_clause)*          %term_var_case

var_clause        <-  "<" _* label _* "=" _* variable _* ">"
                      _* darrow _* term                           %term_var_clause

# ==============================================================================

nil_expr          <-  "nil" _* "[" _* type _* "]"                 %term_nil

cons_expr         <-  "cons" _* "[" _* type _* "]" _*
                      atom _+ atom                                %term_cons

isnil_expr        <-  "isnil" _* "[" _* type _* "]" _* atom       %term_isnil
head_expr         <-  "head" _* "[" _* type _* "]" _* atom        %term_head
tail_expr         <-  "tail" _* "[" _* type _* "]" _* atom        %term_tail

# ==============================================================================

type              <-  function_type
                   /  func_operand

func_operand      <-  sum_type
                   /  sum_part

sum_part          <-  product_type
                   /  product_part

product_part      <-  paren_type
                   /  boolean_type
                   /  numeric_type
                   /  unit_type
                   /  list_type
                   /  base_type
                   /  tuple_type
                   /  record_type
                   /  variant_type

# ==============================================================================

paren_type        <-  "(" _* type _* ")"                          %paren_term

function_type     <-  func_operand _* arrow _* type               %type_func

sum_type          <-  sum_part _* "+" _* func_operand             %type_sum

product_type      <-  product_part _* times _* sum_part           %type_pair

boolean_type      <-  "Bool"                                      %type_bool

numeric_type      <-  "Nat"                                       %type_nat

unit_type         <-  "Unit"                                      %type_unit

list_type         <-  "List" _+ product_part                      %type_list

base_type         <-  [A-Z] [A-Za-z]*                             %type_base

tuple_type        <-  "{" _* type _* ("," _* type _*)+ "}"        %type_tuple

record_type       <-  "{" rt_pair ("," rt_pair)* "}"              %type_record
rt_pair           <-  _* label _* ":" _* type _*

variant_type      <-  "<" rt_pair ("," rt_pair)* ">"              %type_variant
